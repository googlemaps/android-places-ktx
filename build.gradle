/**
 * Copyright 2024 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath libs.gradle
        classpath libs.dokka.gradle.plugin
        classpath libs.kotlin.gradle.plugin
        classpath libs.hilt.android.gradle.plugin
        classpath libs.secrets.gradle.plugin
        classpath libs.jacoco.android
    }
}

plugins {
    id "org.jetbrains.dokka" version "2.0.0"
    id "com.vanniktech.maven.publish" version "0.34.0" apply false
}

ext.projectArtifactId = { project ->
    if (project.name == 'places-ktx') {
        return project.name
    } else {
        return null
    }
}

allprojects {
    group = 'com.google.maps.android'
    // {x-release-please-start-version}
    version = '3.6.0'
    // {x-release-please-end}
    project.ext.artifactId = rootProject.ext.projectArtifactId(project)

    repositories {
        google()
        mavenCentral()
        flatDir {
            dirs 'libs'
        }
    }
}

subprojects { project ->
    if (project.ext.artifactId == null) return

    plugins.apply(libs.plugins.androidLibrary.get().pluginId)
    plugins.apply(libs.plugins.mavenPublish.get().pluginId)
    plugins.apply(libs.plugins.dokka.get().pluginId)
    plugins.apply(libs.plugins.jacocoAndroid.get().pluginId)
    plugins.apply(libs.plugins.vanniktech.get().pluginId)

    jacoco {
        toolVersion = "0.8.7"
    }

    tasks.withType(Test).configureEach {
        jacoco.includeNoLocationClasses = true
        jacoco.excludes = ['jdk.internal.*']
    }

    mavenPublishing {
        publishToMavenCentral()
        signAllPublications()

        pom {
            name = project.name
            description = "Kotlin extensions (KTX) for Places SDK for Android"
            url = "https://github.com/googlemaps/android-places-ktx"

            licenses {
                license {
                    name = "The Apache Software License, Version 2.0"
                    url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution = "repo"
                }
            }

            scm {
                connection = "scm:git@github.com:googlemaps/android-places-ktx.git"
                developerConnection = "scm:git@github.com:googlemaps/android-places-ktx.git"
                url = "https://github.com/googlemaps/android-places-ktx"
            }

            organization {
                name = "Google Inc"
                url = "http://developers.google.com/maps"
            }

            developers {
                developer {
                    id = "google"
                    name = "Google Inc."
                }
            }
        }
    }
}