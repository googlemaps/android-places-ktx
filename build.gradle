/**
 * Copyright 2023 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.9.10'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.22"
        classpath "com.google.dagger:hilt-android-gradle-plugin:2.50"
        classpath "com.google.android.libraries.mapsplatform.secrets-gradle-plugin:secrets-gradle-plugin:2.0.1"
        classpath 'com.mxalbert.gradle:jacoco-android:0.2.1'
    }
}

plugins {
    id "org.jetbrains.dokka" version "1.9.10"
}

ext.projectArtifactId = { project ->
    if (project.name == 'places-ktx') {
        return project.name
    } else {
        return null
    }
}

/**
 * Shared configs across subprojects
 */
allprojects {
    group = 'com.google.maps.android'
    version = '3.1.1'
    project.ext.artifactId = rootProject.ext.projectArtifactId(project)

    repositories {
        google()
        mavenCentral()
        flatDir {
            dirs 'libs'
        }
    }
}

/**
 * Publishing and signing info
 */
subprojects { project ->
    if (project.ext.artifactId == null) return

    apply plugin: 'com.android.library'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'com.mxalbert.gradle.jacoco-android'
    apply plugin: 'signing'

    // Code coverage
    jacoco {
        toolVersion = "0.8.7"
    }

    tasks.withType(Test).configureEach {
        jacoco.includeNoLocationClasses = true
        jacoco.excludes = ['jdk.internal.*']
    }

    tasks.register('sourcesJar', Jar) {
        from android.sourceSets.main.java.source
        archiveClassifier = "sources"
    }

    tasks.register('javadocJar', Jar) {
        dependsOn(tasks.named("dokkaHtml"))
        dependsOn(tasks.named("dokkaJavadoc"))
        archiveClassifier.set("javadoc")
        from new File(buildDir, "dokka/javadoc")
    }

    publishing {
        publications {
            aar(MavenPublication) {
                groupId project.group
                artifactId project.ext.artifactId
                version project.version

                pom {
                    name = project.name
                    description = "Kotlin extensions (KTX) for Places SDK for Android"
                    url = "https://github.com/googlemaps/android-places-ktx"
                    scm {
                        connection = 'scm:git@github.com:googlemaps/android-places-ktx.git'
                        developerConnection = 'scm:git@github.com:googlemaps/android-places-ktx.git'
                        url = 'https://github.com/googlemaps/android-places-ktx'
                    }

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }

                    organization {
                        name = 'Google Inc'
                        url = 'http://developers.google.com/maps'
                    }

                    developers {
                        developer {
                            name = 'Google Inc.'
                        }
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    project.configurations.api.allDependencies.each { dependency ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }

                afterEvaluate {
                    artifact "$buildDir/outputs/aar/$project.name-release.aar"
                    artifact javadocJar
                    artifact sourcesJar
                }
            }
        }

        repositories {
            maven {
                name = "mavencentral"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.aar
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}
